#include <DHT.h>
#include <SoftwareSerial.h>

// Пины
#define DHT_PIN 2
#define LM35_PIN A0
#define PHOTO_PIN A1
#define LED1_PIN 9
#define LED2_PIN 10
#define LED3_PIN 11
#define RELAY_PIN 7
#define BUZZER_PIN 8
#define RGB_R_PIN 3
#define RGB_G_PIN 5
#define RGB_B_PIN 6

// ESP связь
SoftwareSerial espSerial(12, 13); // RX, TX

// DHT
#define DHT_TYPE DHT11
DHT dht(DHT_PIN, DHT_TYPE);

// Переменные состояния
int led1_val = 0, led2_val = 0, led3_val = 0;
int rgb_r = 0, rgb_g = 0, rgb_b = 0;
bool strip_state = false;
bool buzzer_state = false;
unsigned long buzzer_start = 0;
bool melody_playing = false;

void setup() {
  Serial.begin(115200);
  espSerial.begin(9600);
  
  // Пины
  pinMode(LED1_PIN, OUTPUT);
  pinMode(LED2_PIN, OUTPUT);
  pinMode(LED3_PIN, OUTPUT);
  pinMode(RELAY_PIN, OUTPUT);
  pinMode(BUZZER_PIN, OUTPUT);
  pinMode(RGB_R_PIN, OUTPUT);
  pinMode(RGB_G_PIN, OUTPUT);
  pinMode(RGB_B_PIN, OUTPUT);
  
  digitalWrite(RELAY_PIN, LOW);
  digitalWrite(BUZZER_PIN, LOW);
  
  dht.begin();
  
  Serial.println("Arduino готов");
}

void loop() {
  // Читаем команды от ESP
  if (espSerial.available()) {
    String cmd = espSerial.readStringUntil('\n');
    cmd.trim();
    handleCommand(cmd);
  }
  
  // Логика фоторезистора (включаем ленту при темноте)
  int light = analogRead(PHOTO_PIN);
  if (light < 200 && !strip_state) { // Темно
    strip_state = true;
    digitalWrite(RELAY_PIN, HIGH);
    espSerial.println("STRIP:1");
  } else if (light > 300 && strip_state) { // Светло
    strip_state = false;
    digitalWrite(RELAY_PIN, LOW);
    espSerial.println("STRIP:0");
  }
  
  // Управление buzzer мелодией
  if (melody_playing && millis() - buzzer_start > 200) {
    buzzer_state = !buzzer_state;
    digitalWrite(BUZZER_PIN, buzzer_state ? HIGH : LOW);
    buzzer_start = millis();
  }
  
  delay(50);

  if (melody_playing && millis() - buzzer_start > 5000) {
    melody_playing = false;
    digitalWrite(BUZZER_PIN, LOW);
  }
}

void handleCommand(String cmd) {
  Serial.println("Команда: " + cmd);
  
  if (cmd.startsWith("LED1:")) {
    led1_val = cmd.substring(5).toInt();
    analogWrite(LED1_PIN, led1_val);
  }
  else if (cmd.startsWith("LED2:")) {
    led2_val = cmd.substring(5).toInt();
    analogWrite(LED2_PIN, led2_val);
  }
  else if (cmd.startsWith("LED3:")) {
    led3_val = cmd.substring(5).toInt();
    analogWrite(LED3_PIN, led3_val);
  }
  else if (cmd.startsWith("RGB:")) {
    sscanf(cmd.c_str() + 4, "%d,%d,%d", &rgb_r, &rgb_g, &rgb_b);
    analogWrite(RGB_R_PIN, rgb_r);
    analogWrite(RGB_G_PIN, rgb_g);
    analogWrite(RGB_B_PIN, rgb_b);
  }
  else if (cmd.startsWith("STRIP:")) {
    strip_state = cmd.substring(6).toInt();
    digitalWrite(RELAY_PIN, strip_state);
  }
  else if (cmd == "BUZZER") {
    melody_playing = true;
    buzzer_start = millis();
  }
  else if (cmd == "GET_DATA") {
    sendSensorData();
  }
}

void sendSensorData() {
  // DHT11
  float temp_dht = dht.readTemperature();
  float hum_dht = dht.readHumidity();
  
  // LM35
  int lm35_raw = analogRead(LM35_PIN);
  float temp_lm35 = (lm35_raw * 5.0 * 100.0) / 1024.0;
  
  // Фоторезистор
  int light = analogRead(PHOTO_PIN);
  
  // Формируем строку
  String data = "DATA:";
  data += "temp_dht=" + String(temp_dht, 1) + ",";
  data += "hum_dht=" + String(hum_dht, 1) + ",";
  data += "temp_lm35=" + String(temp_lm35, 1) + ",";
  data += "light=" + String(light) + ",";
  data += "led1=" + String(led1_val) + ",";
  data += "led2=" + String(led2_val) + ",";
  data += "led3=" + String(led3_val) + ",";
  data += "rgb_r=" + String(rgb_r) + ",";
  data += "rgb_g=" + String(rgb_g) + ",";
  data += "rgb_b=" + String(rgb_b) + ",";
  data += "strip=" + String(strip_state ? 1 : 0);
  
  espSerial.println(data);
  Serial.println("Отправлены данные: " + data);
}
